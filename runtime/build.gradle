apply plugin: 'com.github.johnrengelman.plugin-shadow'

dependencies {
    for(proj in ['inject', 'http', 'aop']) {
        shadowCompile project(":$proj")
        compile project(":$proj")
    }

    shadowCompile(group:"javax.validation", name:"validation-api", version:validationVersion)
    shadowCompile(group:"com.fasterxml.jackson.core", name:"jackson-databind", version:jacksonVersion)
    shadowCompile(group:"io.reactivex.rxjava2", name:"rxjava", version:rxJava2Version)
    compile(group:"javax.validation", name:"validation-api", version:validationVersion)
    compile(group:"com.fasterxml.jackson.core", name:"jackson-databind", version:jacksonVersion)
    compile(group:"io.reactivex.rxjava2", name:"rxjava", version:rxJava2Version)

    shadow([
            [group:"com.fasterxml.jackson.datatype", name:"jackson-datatype-jdk8", version:jacksonVersion],
            [group:"com.fasterxml.jackson.datatype", name:"jackson-datatype-jsr310", version:jacksonVersion]
    ])

    compileOnly 'org.glassfish:javax.el:3.0.1-b09'
    compileOnly "io.projectreactor:reactor-core:$reactorVersion"
    compileOnly "com.github.akarnokd:rxjava2-interop:$rxJavaInteropVersion"
    compileOnly([group:'com.github.ben-manes.caffeine',name:'caffeine', version:caffeineVersion])
    compileOnly project(":inject-java")
    testCompile "com.github.akarnokd:rxjava2-interop:$rxJavaInteropVersion"
    testCompile "io.projectreactor:reactor-core:$reactorVersion"
    testCompile project(":inject-java")
    testCompile project(":inject-groovy")

    testCompile "org.grails:grails-datastore-gorm:$gormVersion"
}

shadowJar {
    dependencies {
        for(dep in project.configurations.shadowCompile.dependencies) {
            if(dep instanceof ProjectDependency) {
                exclude(project(":$dep.name"))
            }
            else if(dep instanceof ExternalDependency) {
                exclude(dependency("$dep.group:$dep.name:.*"))
            }
        }
        exclude(project(":core"))
        exclude(dependency("com.fasterxml.*:.*:.*"))
        exclude(dependency(group:'org.slf4j',name:'slf4j-api',version:slf4jVersion))
        exclude(dependency(group: 'javax.inject', name: 'javax.inject', version: '1'))
        exclude(dependency('org.ow2.asm:.*:.*'))
        exclude(dependency('org.reactivestreams:.*:.*'))
        exclude(dependency('com.google.code.findbugs:.*:.*'))
        exclude(dependency('com.github.ben-manes.caffeine:.*:.*'))
    }

    relocate "org.objectweb.asm", "io.micronaut.asm"
    relocate "com.github.benmanes.caffeine", "io.micronaut.caffeine"

}
tasks.withType(com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation) { t ->
    t.enabled = false
}

//compileTestGroovy.groovyOptions.forkOptions.jvmArgs = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005']