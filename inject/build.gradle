apply plugin: 'com.github.johnrengelman.plugin-shadow'

shadowJar {
    mergeServiceFiles()
    dependencies {
        exclude(project(":core"))

        exclude(dependency(dependencyVersions.slf4jApi))
        exclude(dependency(group: 'javax.inject', name: 'javax.inject', version: '1'))
        exclude(dependency('org.ow2.asm:.*:.*'))
        exclude(dependency('org.reactivestreams:.*:.*'))
        exclude(dependency('com.google.code.findbugs:.*:.*'))
        exclude(dependency('com.github.ben-manes.caffeine:.*:.*'))
    }
    relocate "org.objectweb.asm", "io.micronaut.asm"
    relocate "com.github.benmanes.caffeine", "io.micronaut.caffeine"
}
tasks.withType(com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation) { t ->
    t.enabled = false
}

dependencies {
    shadowCompile group: 'javax.inject', name: 'javax.inject', version: '1'
    shadowCompile project(":core")

    compile group: 'javax.inject', name: 'javax.inject', version: '1'
    compile project(path: ':core')

    compileOnly dependencyVersions.groovy
    compile dependencyVersions.asm
    compile dependencyVersions.asmCommons
    compile dependencyVersions.asmTree
    compileOnly dependencyVersions.caffeine

    ['javax.validation:validation-api:1.1.0.Final',
     dependencyVersions.yaml].each { String dep ->
        compileOnly dep
        testCompile dep
    }
}